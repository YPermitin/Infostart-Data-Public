
#Область ПрограммныйИнтерфейс

Функция ВыполнитьПроизвольныйСкрипт(Знач ТекстСкрипта) Экспорт
		
	ИнициализацияПромежуточнойТаблицы();
	
	ИдентификаторВызова = Новый УникальныйИдентификатор;
	ИдентификаторВызова = Строка(ИдентификаторВызова);
	
	ТекстСкрипта = СтрЗаменить(ТекстСкрипта, "&ИдентификаторВызова", ИдентификаторВызова);
	
	ВнешниеИсточникиДанных.ПримерИсточникаДанных.ExecCustomCommand(ТекстСкрипта);	
	
	Возврат ПолучитьРезультатВызова(ИдентификаторВызова, Истина);
	
КонецФункции

#КонецОбласти

#Область Служебный

Процедура ИнициализацияПромежуточнойТаблицы()
	
	КомандаИнициализацииПромежуточнойТаблицы = "
	|IF(OBJECT_ID('tempdb..##CallsAndValues_ExternalDataSource') IS NULL)
	|BEGIN
	|	BEGIN TRY 
	|		CREATE TABLE ##CallsAndValues_ExternalDataSource
	|		(
	|			-- Поле идентификатора вызова (какой-то GUID)
	|			CallId varchar(36) NOT NULL,
	|			-- Результат в виде текста
	|			Result nvarchar(max) NULL
	|		)
	|		-- Создаем индекс для оптимизации поиска
	|		CREATE UNIQUE CLUSTERED INDEX [_indx1] ON ##CallsAndValues_ExternalDataSource
	|		(
	|			[CallId] ASC
	|		)
	|	END TRY 
	|	BEGIN CATCH 
	|		-- При ошибке ничего не делаем
	|	END CATCH  
	|END
	|";
	
	ВнешниеИсточникиДанных.ПримерИсточникаДанных.ExecCustomCommand(КомандаИнициализацииПромежуточнойТаблицы);	
	
КонецПроцедуры

Функция ПолучитьРезультатВызова(ИдентификаторВызова, УдалитьЗначениеПослеПолучения = Ложь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	CallsAndValues.Result КАК Result
		|ИЗ
		|	ВнешнийИсточникДанных.ПримерИсточникаДанных.Таблица.CallsAndValues КАК CallsAndValues
		|ГДЕ
		|	CallsAndValues.ID = &ID";	
	Запрос.УстановитьПараметр("ID", ИдентификаторВызова);	
	РезультатЗапроса = Запрос.Выполнить();	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если УдалитьЗначениеПослеПолучения Тогда
		УдалитьЗначениеВызова(ИдентификаторВызова);
	КонецЕсли;
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Result;
	Иначе
		Возврат Null;
	КонецЕсли;
	
КонецФункции

Процедура УдалитьЗначениеВызова(ИдентификаторВызова)
	
	ТекстКоманды = "
	|DECLARE @sql nvarchar(max) = 'DELETE FROM ##CallsAndValues_ExternalDataSource WHERE CallId = @CallId';
	|EXECUTE sp_executesql @sql, N'@CallId varchar(36)', @CallId = '" + ИдентификаторВызова + "'
	|";
	
	ВнешниеИсточникиДанных.ПримерИсточникаДанных.ExecCustomCommand(ТекстКоманды);
	
КонецПроцедуры

#КонецОбласти
