
&НаСервере
Процедура ПроверитьСвойствоДокумента()
	
	РеквизитыДокумента = Метаданные.Документы.ТестовыйДокумент.Реквизиты;
	ИмяРеквизита = "Комментарий";
	
	Если ПеременнаяСодержитСвойство(РеквизитыДокумента, ИмяРеквизита) Тогда
		Сообщить("Реквизит """ + ИмяРеквизита + """ найден!"); 
	Иначе
		Сообщить("Реквизит """ + ИмяРеквизита + """ НЕ найден!");
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСвойство(Команда)
	
	ПроверитьСвойствоДокумента();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПеременнаяСодержитСвойство(Переменная, ИмяСвойства)
	
	Если НЕ ЗначениеЗаполнено(Переменная) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Инициализируем структуру для теста 
	// с ключом (значение переменной "ИмяСвойства") 
	// и значением произвольного GUID'а
	GUIDПроверка = Новый УникальныйИдентификатор;
	СтруктураПроверка = Новый Структура;
	СтруктураПроверка.Вставить(ИмяСвойства, GUIDПроверка);
	
	// Заполняем созданную структуру из переданного 
	// значения переменной
	ЗаполнитьЗначенияСвойств(СтруктураПроверка, Переменная);
	
	// Если значение для свойства структуры осталось 
	// NULL, то искомое свойство не найдено, 
	// и наоборот.
	
	Если СтруктураПроверка[ИмяСвойства] = GUIDПроверка Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции
