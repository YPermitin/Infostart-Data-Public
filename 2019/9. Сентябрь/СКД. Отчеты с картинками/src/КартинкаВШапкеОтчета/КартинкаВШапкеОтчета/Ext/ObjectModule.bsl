
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; 	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();  
	
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;      		
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;    	
	
	СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки);
	
	ДокументРезультат.Очистить();
	
	// Получаем дополнительный макет с описанием вывода логотипа
	МакетЛоготип = ПолучитьМакет("ЛоготипКомпании"); 
	// Получаем область верхнего уровня для сворачивания логотипа
	ОбластьСвернуть = МакетЛоготип.ПолучитьОбласть("СвернутьЛоготип");
	// Получаем область логотипа
	Область = МакетЛоготип.ПолучитьОбласть("Логотип");
	
	ДокументРезультат.НачатьАвтогруппировкуСтрок();   //Начало группировки строк
	ДокументРезультат.Вывести(ОбластьСвернуть, 1);    // Вывод строки группировки на 1 уровне
	ДокументРезультат.Вывести(Область,         2);    // Вывод логотипа на втором уровне группировки
	ДокументРезультат.ЗакончитьАвтогруппировкуСтрок();//Завершение группировки строк
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;	
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);		
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
				
КонецПроцедуры
